package funkin.ui.debug;

import openfl.text.TextField;
import openfl.text.TextFormat;
import openfl.system.System;

/**
  The FPS class provides an easy-to-use monitor to display
  the current frame rate of an OpenFL project
**/
class StatisticMonitor extends TextField
{
  public var currentFPS(default, null):Int;
  public var memoryMegas(get, never):Float;

  @:noCompletion private var times:Array<Float>;

  public function new(x:Float = 10, y:Float = 10, color:Int = 0x000000)
  {
    super();

    this.x = x;
    this.y = y;

    currentFPS = 0;
    selectable = mouseEnabled = false;
    defaultTextFormat = new TextFormat("VCR OSD Mono", 14, color);
    autoSize = LEFT;
    multiline = true;
    text = "";

    times = [];
  }

  var deltaTimeout:Float = 0.0;

  private override function __enterFrame(deltaTime:Float):Void
  {
    if (!visible) return;

    if (deltaTimeout > 1000)
    {
      deltaTimeout = 0.0;
      return;
    }

    final now:Float = haxe.Timer.stamp() * 1000;
    times.push(now);
    while (times[0] < now - 1000)
      times.shift();

    currentFPS = times.length < FlxG.updateFramerate ? times.length : FlxG.updateFramerate;
    deltaTimeout += deltaTime;

    text = "FPS • " + currentFPS + '\nMemory • ' + flixel.util.FlxStringUtil.formatBytes(memoryMegas);
    // + '\n' + lime.app.Application.current.meta.get('version');
  }

  inline function get_memoryMegas():Float
    return cast(System.totalMemory, UInt);
}
